---
page_title: "truenas_app Resource - terraform-provider-truenas"
subcategory: ""
description: |-
  Manages a TrueNAS application (custom Docker Compose app or catalog app).
---

# truenas_app (Resource)

Manages a TrueNAS application (custom Docker Compose app or catalog app).

## Example Usage

### Basic Custom App

```terraform
# Create a custom Docker Compose app
resource "truenas_app" "nginx" {
  name       = "nginx"
  custom_app = true

  compose_config = <<-EOT
    services:
      nginx:
        image: nginx:latest
        ports:
          - "8080:80"
        volumes:
          - /mnt/tank/apps/nginx/config:/etc/nginx/conf.d
          - /mnt/tank/apps/nginx/html:/usr/share/nginx/html
        restart: unless-stopped
  EOT
}

# Output the app state
output "nginx_state" {
  value = truenas_app.nginx.state
}
```

### App with File Dependencies

Use `restart_triggers` to automatically restart the app when dependent files change:

```terraform
resource "truenas_host_path" "app_config" {
  name = "my-app-config"
  path = "/mnt/data/my-app"
}

resource "truenas_file" "config" {
  host_path     = truenas_host_path.app_config.id
  relative_path = "config.yaml"
  content       = file("config.yaml")
}

resource "truenas_app" "example" {
  name       = "my-app"
  custom_app = true

  compose_config = <<-EOF
    services:
      web:
        image: nginx:latest
        volumes:
          - ${truenas_host_path.app_config.path}:/config:ro
  EOF

  # When the config file changes, restart the app to pick up the new config
  restart_triggers = {
    config_checksum = truenas_file.config.checksum
  }
}
```

> **Note:** Adding or removing `restart_triggers` does not trigger a restart. Only changes to existing trigger values cause the app to restart.

## Import

Apps can be imported using the app name:

```shell
terraform import truenas_app.example my-app
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `custom_app` (Boolean) Whether this is a custom Docker Compose application.
- `name` (String) Application name.

### Optional

- `compose_config` (String) Docker Compose YAML configuration string (required for custom apps).
- `desired_state` (String) Desired application state: 'running' or 'stopped' (case-insensitive). Defaults to 'RUNNING'.
- `restart_triggers` (Map of String) Map of values that, when changed, trigger an app restart. Use this to restart the app when dependent resources change, e.g., `restart_triggers = { config_checksum = truenas_file.config.checksum }`.
- `state_timeout` (Number) Timeout in seconds to wait for state transitions. Defaults to 120. Range: 30-600.

### Read-Only

- `id` (String) Application identifier (the app name).
- `state` (String) Application state (RUNNING, STOPPED, etc.).
